command /shop:
    trigger:
        set {_i} to chest inventory with 5 rows named "Shop GUI" 
        set slot (integers from 0 to 44) of {_i} to black stained glass pane named " "
        set slot 10 of {_i} to cobblestone named "&a&lBlocks Category"
        set slot 12 of {_i} to white stained glass block named "&a&lGenerators Category"
        set slot 14 of {_i} to oak wood door named "&a&lMisc Category"
        set slot 16 of {_i} to tnt named "&a&lExplosives Category"
        set slot 30 of {_i} to iron chestplate named "&a&lArmor Category" with all flags hidden
        set slot 32 of {_i} to iron sword named "&a&lWeapons Category" with all flags hidden
        open {_i} to player 


on inventory click:
    if name of event-inventory = "Shop GUI":

        # Blocks 
        if clicked slot = 10:
            set {_i} to chest inventory with 3 rows named "Shop GUI: &a&lBlocks"
            SetUpPage({_i}, 3, true)

            set slot 10 of {_i} to 16 white wool named "&a&l16x Wool" with lore " &7Cost: &6$10", "" and CanPurchase(uuid of player, 1, 0)
            set slot 11 of {_i} to 16 cobblestone named "&a&l16x Cobblestone" with lore " &7Cost: &6$20", "" and CanPurchase(uuid of player, 1, 1)
            set slot 12 of {_i} to 16 iron block named "&a&l16x Iron Blocks" with lore " &7Cost: &6$30", "" and CanPurchase(uuid of player, 1, 2)
            open {_i} to player

        # Generators Category
        else if clicked slot = 12:
            set {_i} to chest inventory with 3 rows named "Shop GUI: &a&lGenerators"
            SetUpPage({_i}, 3, true)
            set slot 13 of {_i} to white stained glass block named "&a&lTier 1 Generator" with lore " &7Cost: &6$10", "" and "&aClick to purchase!"
            open {_i} to player 

        # Misc Category
        else if clicked slot = 14:
            set {_i} to chest inventory with 4 rows named "Shop GUI: &a&lMisc"
            SetUpPage({_i}, 4, true)
                
            set slot 10 of {_i} to oak door named "&a&l1x Oak Door" with lore " &7Cost: &6$10", "" and CanPurchase(uuid of player, 1, 0)
            set slot 19 of {_i} to oak trapdoor named "&a&l1x Oak Trapdoor" with lore " &7Cost: &6$10", "" and CanPurchase(uuid of player, 1, 0)

            set slot 11 of {_i} to dark oak door named "&a&l1x Dark Oak Door" with lore " &7Cost: &6$20", "" and CanPurchase(uuid of player, 1, 1)
            set slot 20 of {_i} to dark oak trapdoor named "&a&l1x Dark Oak Trapdoor" with lore " &7Cost: &6$20", "" and CanPurchase(uuid of player, 1, 1)

            set slot 12 of {_i} to iron door named "&a&l1x Iron Door" with lore " &7Cost: &6$30", "" and CanPurchase(uuid of player, 1, 2)
            set slot 21 of {_i} to iron trapdoor named "&a&l1x Iron Trapdoor" with lore " &7Cost: &6$30", "" and CanPurchase(uuid of player, 1, 2)

            open {_i} to player

        # Explosives Category
        else if clicked slot = 16:
            set {_i} to chest inventory with 3 rows named "Shop GUI: &a&lExplosives"
            SetUpPage({_i}, 3, true)

            set {_n} to 10
            loop 5 times:
                set slot {_n} of {_i} to {explosives::item} named "&a&lTier %loop-number% Explosive" with lore " &7Cost: &6$1", "" and CanPurchase(uuid of player, 10, (loop-number - 1))
                add 1 to {_n}

            open {_i} to player

        # Armour
        else if clicked slot = 30:

            ArmorPage(player)

        # Weapons Category
        else if clicked slot = 32:
            set {_i} to chest inventory with 3 rows named "Shop GUI: &a&lWeapons"
            SetUpPage({_i}, 3, true)

            set slot 10 of {_i} to stone sword named "&a&lStone Sword" with lore " &7Cost: &6$1", "" and CanPurchase(uuid of player, 1, 0) with all flags hidden 
            set slot 11 of {_i} to iron sword named "&a&lIron Sword" with lore " &7Cost: &6$1", "" and CanPurchase(uuid of player, 1, 1) with all flags hidden 
            set slot 12 of {_i} to diamond sword named "&a&lDiamond Sword" with lore " &7Cost: &6$1", "" and CanPurchase(uuid of player, 1, 2) with all flags hidden
            set slot 13 of {_i} to fishing rod named "&a&lFishing Rod" with lore " &7Cost: &6$1", "" and CanPurchase(uuid of player, 1, 3) 
            set slot 14 of {_i} to bow named "&a&lBow" with lore " &7Cost: &6$1", "" and CanPurchase(uuid of player, 1, 4) with all flags hidden 
            set slot 15 of {_i} to bow of power 1 named "&a&lBow &8(&f&lPower 1&8)" with lore " &7Cost: &6$1", "" and CanPurchase(uuid of player, 1, 5) with all flags hidden 
            set slot 16 of {_i} to 16 arrows named "&a&l16x Arrow" with lore " &7Cost: &6$1", "" and CanPurchase(uuid of player, 1, 0)

            open {_i} to player 

    # Going back 
    if name of event-inventory contains "Shop GUI":
        cancel event
        if event-slot = arrow:
            execute player command "/shop"

        if event-slot != black stained glass pane named " ", arrow named "&c&lGo Back" or air:

            if uncoloured name of event-inventory = "Shop GUI: Armor":
                set {_armorType::*} to uncoloured name of event-slot split at " "
                loop {_armorType::*}:
                    set {_protection} to " of protection 1" if loop-value = "(Protection"
                ArmorIndividualPage(player, {_armorType::1}, ({_protection} ? ""))     

            # Check if item is unlocked
            else if (first 2 characters of line 3 of lore of event-slot) = "&c":

                set {_cost::*} to uncoloured line 1 of lore of event-slot parsed as " Cost\:\ $%number%"
                if player's balance >= {_cost::1}:

                    remove {_cost::1} from player's balance 
                    send "&7Successfully purchased &a&l%name of event-slot%"

                    # custom items
                    if name of event-inventory contains "Explosives":
                        set {_tier::*} to uncoloured name of event-slot parsed as "Tier %number% Explosive"
                        GiveExplosive(player, {_tier::1})
                    
                    # regular items
                    else:
                        set {_item} to "%item amount of event-slot% of %type of event-slot%" parsed as item
                        enchant {_item} with enchantments of event-slot 
                        give {_item} to player 

                else:
                    send "&cYou can not afford this item!"

            else:
                send "&cYou have not unlocked this item!"

function ArmorPage(p: player):

    set {_u} to uuid of {_p}

    set {_i} to chest inventory with 4 rows named "Shop GUI: &a&lArmor"
    SetUpPage({_i}, 4, true)

    set slot 10 of {_i} to leather chestplate named "&a&lLeather Armor" with lore " &7Click to purchase armor pieces", "" and CanPurchase({_u}, 1, 0) with all flags hidden
    set slot 11 of {_i} to leather chestplate of protection 1 named "&a&lLeather Armor &8(&f&lProtection 1&8)" with lore " &7Click to purchase armor pieces", "" and CanPurchase({_u}, 1, 1) with all flags hidden
    set slot 12 of {_i} to chainmail chestplate named "&a&lChainmail Armor" with lore " &7Click to purchase armor pieces", "" and CanPurchase({_u}, 1, 2) with all flags hidden
    set slot 13 of {_i} to chainmail chestplate of protection 1 named "&a&lChainmail Armor &8(&f&lProtection 1&8)" with lore " &7Click to purchase armor pieces", "" and CanPurchase({_u}, 1, 3) with all flags hidden
    set slot 14 of {_i} to iron chestplate named "&a&lIron Armor" with lore " &7Click to purchase armor pieces", "" and CanPurchase({_u}, 1, 4) with all flags hidden
    set slot 15 of {_i} to iron chestplate of protection 1 named "&a&lIron Armor &8(&f&lProtection 1&8)" with lore " &7Click to purchase armor pieces", "" and CanPurchase({_u}, 1, 5) with all flags hidden
    set slot 16 of {_i} to diamond chestplate named "&a&lDiamond Armor" with lore " &7Click to purchase armor pieces", "" and CanPurchase({_u}, 1, 6) with all flags hidden
    set slot 19 of {_i} to diamond chestplate of protection 1 named "&a&lDiamond Armor &8(&f&lProtection 1&8)" with lore " &7Click to purchase armor pieces", "" and CanPurchase({_u}, 1, 7) with all flags hidden

    open {_i} to {_p}

function ArmorIndividualPage(p: player, type: text, protection: text):  

    set {_u} to uuid of {_p}

    if {_protection} != "":
        set {_nameProt} to " &8(&f&lProtection 1&8)"
    else:
        set {_nameProt} to ""

    set {_n} to 0
    loop ("leather", "chainmail", "iron" and "diamond"):
        if {_type} = loop-value:
            set {_prestige} to {_n} * 2
            if {_protection} != "":
                add 1 to {_prestige}
            exit loop
        add 1 to {_n}


    set {_i} to chest inventory with 3 rows named "Shop GUI: &a&l%{_type}% Armor"
    SetUpPage({_i}, 3, true)
    set slot 10 of {_i} to ("%{_type}% helmet%{_protection}%" parsed as itemtype) named "&a&l%{_type}% Helmet%{_nameProt}%" with lore " &7Cost: &6$%300 * ({_prestige} + 1)^3%", "" and CanPurchase({_u}, 1, {_prestige}) with all flags hidden
    set slot 11 of {_i} to ("%{_type}% chestplate%{_protection}%" parsed as itemtype) named "&a&l%{_type}% Helmet%{_nameProt}%" with lore " &7Cost: &6$%500 * ({_prestige} + 1)^3%", "" and CanPurchase({_u}, 2, {_prestige}) with all flags hidden
    set slot 12 of {_i} to ("%{_type}% leggings%{_protection}%" parsed as itemtype) named "&a&l%{_type}% Helmet%{_nameProt}%" with lore " &7Cost: &6$%400 * ({_prestige} + 1)^3%", "" and CanPurchase({_u}, 4, {_prestige}) with all flags hidden
    set slot 13 of {_i} to ("%{_type}% boots%{_protection}%" parsed as itemtype) named "&a&l%{_type}% Helmet%{_nameProt}%" with lore " &7Cost: &6$%200 * ({_prestige} + 1)^3%", "" and CanPurchase({_u}, 6, {_prestige}) with all flags hidden
    open {_i} to {_p}